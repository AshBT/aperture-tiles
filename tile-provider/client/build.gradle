import org.apache.tools.ant.filters.ReplaceTokens

description = "A client (and pieces in common to both client and server) of a RabbitMQ-based tile provider"

// Configure for scala compilation via the scala plugin
apply plugin: "scala"

// appends scala test functionality to the baseline test task
test << {
	ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: classpath.asPath)
	ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false') {
		reporter(type: 'file', filename: 'test.output')
	}
}

// Task to create a JAR from all source (scala and java)
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource, sourceSets.test.allSource
}

// Task to create a scaladoc JAR
task scaladocJar(type: Jar, dependsOn: scaladoc) {
	classifier = "scaladoc"
	from scaladoc.destinationDir
}

// Task to create a javadoc JAR
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
}

// Task to create a jar of test classes
task testJar(type: Jar) {
	classifier = "tests"
	from sourceSets.test.output
}

// produce artifacts using the tasks above
artifacts {
	archives sourcesJar
	archives scaladocJar
	archives javadocJar
	archives testJar
}

// By default the Gradle scala plugin builds java code before scala code.  This leads to problems
// in our setup because the scala code in this project is used by the java code (causing
// compile errors).  We force the plugin to use an empty source set for java, and then set the
// scala source to both scala and java.  The scala compiler generates class files for both without
// issue.  This is a bit of hack, and can be fixed by re-organizing our code so that we don't mix
// scala and java in the same project.
sourceSets {
	main {
		scala {
			srcDirs = ["src/main/scala", "src/main/java"]
		}
		java {
			srcDirs = []
		}
	}
}

// Jars / projects this project depends on.
dependencies {
	// Compile config - needed to build
	compile "org.scala-lang:scala-library:$scalaVersion"
	compile "org.scala-lang:scala-reflect:$scalaVersion"
	compile "org.clapper:grizzled-slf4j_2.10:1.0.2"
	compile "com.rabbitmq:amqp-client:3.5.3"
	compile project(":binning-utilities")
	compile project(":geometric-utilities")
	testCompile "org.scalatest:scalatest_$dependencyScalaVersion:2.1.1"
	testCompile project(path: ':binning-utilities', configuration: 'tests')
}
